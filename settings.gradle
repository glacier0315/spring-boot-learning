plugins {
    // Apply the foojay-resolver plugin to allow automatic download of JDKs
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

rootProject.name = 'spring-boot-learning'

//includeBuild('platforms')
//include('web:api-manage:openapi:demo1')
//include('web:auth:shiro:demo1')
//include('web:auth:shiro:demo3')
//include('web:auth:shiro:demo4')
//include('web:auth:shiro:jdbc1')
//include('web:auth:spring-security:jdbc-login1')
//include('web:auth:spring-security:memory-login1')
//include('web:auth:spring-security:memory-login2')
//include('web:container-image:docker:demo1')
//include('web:data-access-layer:elasticsearch:demo1')
//include('web:data-access-layer:flyway:druid1')
//include('web:data-access-layer:jasypt:druid1')
//include('web:data-access-layer:mybatis:druid1')
//include('web:data-access-layer:mybatis:hikari1')
//include('web:data-access-layer:mybatis-plus:demo1')
//include('web:data-access-layer:mybatis-plus:dynamic1')
//include('web:data-access-layer:redis:demo1')
//include('web:data-access-layer:spring-data-jpa:demo1')
//include('web:document-processing:easyexcel1')
//include('web:document-processing:hutool-poi1')
//include('web:document-processing:office-to-pdf1')
//include('web:features:aop:demo1')
//include('web:features:async:demo1')
//include('web:features:config-order:demo1')
//include('web:features:event:demo1')
//include('web:features:https:demo1')
//include('web:features:jar2war')
//include('web:features:validation:demo1')
//include('web:features:websocket:demo1')
//include('web:file-service:fastdfs:demo1')
//include('web:logging:logback:demo1')
//include('web:message-queuing:kafka:demo1')
//include('web:message-queuing:rabbitmq:demo1')
//include('web:message-queuing:rocketmq:demo1')
//include('web:monitor:spring-boot-actuator:demo1')
//include('web:monitor:spring-boot-admin:client1')
//include('web:monitor:spring-boot-admin:server1')
//include('web:restful-service:jersey:demo1')
//include('web:scheduled-tasks:quartz:demo1')
//include('web:scheduled-tasks:quartz:demo2')
//include('web:scheduled-tasks:scheduled:demo1')
//include('web:template-engine:freemarker:demo1')
//include('web:template-engine:thymeleaf:AdminLTE_CN')
//include('web:template-engine:thymeleaf:demo1')
//include('web:template-engine:thymeleaf:demo2')
//include('web:template-engine:thymeleaf:generation')
//include('web:template-engine:thymeleaf:Pear-Admin-Layui')
//include('web:workflow:activiti6:demo1')
//include('web:workflow:activiti7:demo1')
//include('web:workflow:camunda:camunda-web1')
//include('webflux')


/**
 * 判断目录是否忽略
 * @param file 目标目录
 * @return
 */
static def isIgnored(File file) {
    // 忽略目录
    def ignoreDirs = ["buildSrc", "gradle", "src", "build"]
    return file.getName().startsWith(".") || ignoreDirs.contains(file.getName())
}

/**
 * 判断目录是否含有 集合中的文件
 */
static def hasFiles(File file, List<String> fileNames) {
    return file.isDirectory() &&
            file.listFiles({ !it.isDirectory() } as FileFilter)
                    .any { fileNames.contains(it.getName()) }
}

/**
 * 判断目录是否含有 settings.gradle
 */
static def hasSettings(File file) {
    return hasFiles(file, ["settings.gradle", "settings.gradle.kts"])
}

/**
 * 判断目录是否含有 build.gradle
 */
static def hasBuildFile(File file) {
    return hasFiles(file, ["build.gradle", "build.gradle.kts"])
}

/**
 * 自动注册 includeBuild
 * @param dirs 目标检查目录
 * @return
 */
def autoRegisterIncludeBuild(File dir) {
    dir.eachFile {
        if (it.isDirectory() && !isIgnored(it) && hasSettings(it)) {
            println "includeBuild('${it.getName()}')"
            includeBuild(it.getName())
        }
    }
}

/**
 * 递归找出目录下所有含有 build.gradle 的目录
 * @param files 目标目录
 * @return
 */
static def findHasBuildFile(File[] files) {
    List<String> modules = []
    files.each {
        // 排除忽略文件
        if (isIgnored(it)) return
        if (hasBuildFile(it)) {
            modules.add(it.getPath())
        }
        modules.addAll(findHasBuildFile(it.listFiles({ it.isDirectory() && !isIgnored(it) && !hasSettings(it) } as FileFilter)));
    }
    return modules
}

/**
 * 自动注册 include
 * @param dirs 目标检查目录
 * @return
 */
def autoRegisterInclude(File dirs) {
    // 找出目标目录
    List<String> modules = findHasBuildFile(dirs.listFiles({ it.isDirectory() && !isIgnored(it) && !hasSettings(it) } as FileFilter))

    modules.stream()
            .filter({ s -> !modules.any { it.startsWith(s) && it != s } })
            .each {
                // 组装模块
                def modelName = it.substring(dirs.getPath().length() + 1)
                        .replace(File.separator, ":")
                println "include('$modelName')"
                include(modelName)
            }
}
// 自动注册 includeBuild
autoRegisterIncludeBuild(settingsDir)

// 自动注册 include
autoRegisterInclude(settingsDir)


