plugins {
    id 'java'
}

// 指定仓库
repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://maven.aliyun.com/repository/jcenter' }
    maven { url 'https://maven.aliyun.com/repository/google' }
    maven { url 'https://maven.aliyun.com/repository/spring' }
    maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
    }
}

configurations {
    // 全局排除依赖
    all*.exclude group: "junit", module: "junit"
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform() {
        excludeEngines 'junit-vintage'
    }
}

// 设置编码为UTF-8
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    doFirst {
        options.compilerArgs += [
                '--module-path', classpath.asPath
        ]
    }
}

// Copy LICENSE
tasks.withType(Jar).configureEach {
    from(project.rootDir) {
        include 'LICENSE'
        into 'META-INF'
    }
}

// 配置 创建源文件夹
tasks.register("mkdirs") {
    sourceSets*.java.srcDirs*.forEach(f -> {
        f.mkdirs()
    })
    sourceSets*.resources.srcDirs*.forEach(f -> {
        f.mkdirs()
    })
}

// 拷贝jar包
tasks.register('copyJars', Copy) {
    // 运行时
    from configurations.runtimeClasspath
    // 目标位置
    into "$buildDir/jars"
}

//配置 删除文件夹
def cleanTempFiles = tasks.register("cleanTempFiles") {
    doLast {
        delete "out"
        delete "logs"
        delete "bin"
        delete ".settings"
        delete ".classpath"
        delete ".project"
    }
}

tasks.named("clean") {
    dependsOn(cleanTempFiles)
}
