plugins {
    id 'java'
}

// 指定仓库
repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://maven.aliyun.com/repository/jcenter' }
    maven { url 'https://maven.aliyun.com/repository/google' }
    maven { url 'https://maven.aliyun.com/repository/spring' }
    maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    mavenCentral()
}


import java.time.OffsetDateTime
import java.time.format.DateTimeFormatter

// 取当前时间
def buildTimeAndDate = OffsetDateTime.now()

// 根据时间生成字符串变量
ext {
    projectVersion = project.version
    buildDate = DateTimeFormatter.ISO_LOCAL_DATE.format(buildTimeAndDate)
    buildTime = DateTimeFormatter.ofPattern('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    manifestMap = [
            'Created-By'            : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
            'Built-By'              : 'gradle',
            'Build-Date'            : buildDate,
            'Build-Time'            : buildTime,
            'Built-OS'              : "${System.properties['os.name']}",
            'Specification-Title'   : project.name,
            'Specification-Version' : project.version,
            'Specification-Vendor'  : 'glacier',
            'Implementation-Title'  : project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor' : 'glacier'
    ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
    }
}

configurations {
    // 全局排除依赖
    all*.exclude group: "junit", module: "junit"
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform() {
        excludeEngines 'junit-vintage'
    }
}

// 设置编码为UTF-8
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    doFirst {
        options.compilerArgs += [
                '--module-path', classpath.asPath
        ]
    }
}

// Copy LICENSE
tasks.withType(Jar) {
    from(project.rootDir) {
        include 'LICENSE'
        into 'META-INF'
    }
}

// 配置 创建源文件夹
task mkdirs {
    sourceSets*.java.srcDirs*.forEach(f -> {
        f.mkdirs()
    })
    sourceSets*.resources.srcDirs*.forEach(f -> {
        f.mkdirs()
    })
}

// 拷贝jar包
task copyJars(type: Copy) {
    // 运行时
    from configurations.runtimeClasspath
    // 目标位置
    into "$buildDir/jars"
}

//配置 删除文件夹
task cleanTempFiles {
    doLast {
        delete "out"
        delete "logs"
        delete "bin"
        delete ".settings"
        delete ".classpath"
        delete ".project"
    }
}
clean.dependsOn cleanTempFiles

// 生成jar文件时，MANIFEST.MF的内容如下
jar {
    manifest {
        attributes(manifestMap)
    }
}
